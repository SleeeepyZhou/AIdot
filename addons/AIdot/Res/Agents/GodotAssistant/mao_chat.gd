@tool
extends TabContainer

const Default_Model_path = "user://maochat/maochat.tres"

func _ready() -> void:
	chat_bot.chat_completed.connect(call_back)
	_on_type_pressed()
	if FileAccess.file_exists(Default_Model_path):
		chat_bot.model = load(Default_Model_path)
		url.text = chat_bot.model.api_url
		key.text = chat_bot.model.api_key
		type.text = ModelLayer.get_type(chat_bot.model.model_name)
		llm.text = chat_bot.model.model_name


# MaoChat
@onready var chat_bot: ChatAgent = $MaoChat/MaoChat/ScrollContainer/ChatBot
@onready var chat_list: VBoxContainer = $MaoChat/MaoChat/ScrollContainer/ChatList
const CHAT_UNIT = preload("res://addons/AIdot/Res/Agents/GodotAssistant/ChatUnit.tscn")

@onready var user_agent: UserAgent = $MaoChat/MaoChat/Input/Input/UserAgent
@onready var input_box: TextEdit = $MaoChat/MaoChat/Input/Input/InputBox
@onready var send: Button = $MaoChat/MaoChat/Input/Input/Send
@onready var idle_pan: PanelContainer = $MaoChat/MaoChat/Input/IdlePan
@onready var idle: RichTextLabel = $MaoChat/MaoChat/Input/IdlePan/Idle

const idle_effects = [
	"[pulse freq=1.0 color=#ffffff40 ease=-2.0]{0}[/pulse]",
	"[wave amp=50.0 freq=5.0 connected=1]{0}[/wave]",
	"[shake rate=20.0 level=5 connected=1]{0}[/shake]",
	"[tornado radius=10.0 freq=1.0 connected=1]{0}[/tornado]",
	"[rainbow freq=1.0 sat=0.8 val=0.8]{0}[/rainbow]",
]
const rainbow_phrases = [
	"ğŸŒˆ æ­£åœ¨ç©¿è¶Šå½©è™¹éš§é“... 98%", 
	"ğŸ“¡ é‡å­æ³¢åŠ¨é€Ÿè¯»ä¸­... â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–‘â–‘â–‘",
	"ğŸš€ æ­£åœ¨å‡†å¤‡è·ƒè¿è·³è·ƒ... å€’è®¡æ—¶3ç§’",
	"ğŸ•¶ï¸ æ­£åœ¨è§£ç å…ƒå®‡å®™å…¥å£... éªŒè¯é€šè¿‡",
	"ğŸ® å¯åŠ¨äº‘æ¸¸æˆåè®®... ğŸ•¹ï¸ åŠ è½½æˆå°±ç³»ç»Ÿ",
	"ğŸ”­ æ˜Ÿé™…å¯¼èˆªæ ¡å‡†ä¸­... ğŸŒŒ å‘ç°æ–°æ˜Ÿé—¨",
	"ğŸ¤– AIè¯—äººæ­£åœ¨åˆ›ä½œ... âœï¸ ç”Ÿæˆå½©è™¹åå››è¡Œè¯—",
	"ğŸ§¬ æ­£åœ¨å…‹éš†å¿«ä¹åŸºå› ... âš—ï¸ åŸ¹å…»æ¶²æµ“åº¦æ­£å¸¸",
	"ğŸ›¸ æ¥æ”¶å¤–æ˜Ÿå¿«é€’ä¸­... ğŸ“¦ åŒ…è£¹å·²æ¶ˆæ¯’",
	"ğŸ¨ æ¸²æŸ“è™šæ‹Ÿå½©è™¹... ğŸ–Œï¸ æ­£åœ¨æ··åˆä¸ƒç§å…‰æ³¢",
	"ğŸŒ€ ç ´è§£æ¬¡å…ƒå£... ğŸ’¥ å‘ç°éšè—å…³å¡",
	"ğŸ”® é¢„æµ‹æœªæ¥å¤©æ°”... â˜€ï¸ æ˜æ—¥å°†å‡ºç°å¤ªé˜³é›¨",
	"ğŸ§¿ å¯åŠ¨å¹¸è¿å¢å¹…å™¨... ğŸ± é»‘çŒ«è½¬åŒ–ç™½çŒ«ç¨‹åº",
	"ğŸª åŒæ­¥è½¨é“ç©ºé—´ç«™... ğŸ‘¨ğŸš€ å®‡èˆªå‘˜è¡¨æƒ…åŒ…å·²æ›´æ–°",
	"ğŸ² æ­£åœ¨æ·å‘½è¿éª°å­... ğŸ¯ ç»“æœä¸ºï¼šå¤§å‰",
	"ğŸ¦„ æ•æ‰å¹»å…½å¹¼å´½... ğŸ¬ ä½¿ç”¨å½©è™¹è½¯ç³–è¯±æ•",
	"âŒ› æ—¶ç©ºè¤¶çš±ä¿®å¤ä¸­... â³ ç»´æŒå› æœå¾‹ç¨³å®š",
	"ğŸŸ é‡å­é”¦é²¤åŠ è½½ä¸­... ğŸ’« å¥½è¿buffå·²å åŠ ",
	"ğŸ§Š æ­£åœ¨èåŒ–å†°æ²³ä¸–çºª... ğŸŒ¸ å²å‰èŠ±æœµå³å°†ç»½æ”¾",
	"ğŸ­ åˆ‡æ¢äººæ ¼é¢å…·... ğŸ¤¹ å‡†å¤‡å¤šå…ƒæ¼”ç»",
	"ğŸ¤¯ ä¸‰ä½“é—®é¢˜ç®€åŒ–ä¸º... ğŸ’¡ åŒå‡»ç‚¹äº®å¤ªé˜³",
	"ğŸµ æ­£åœ¨æ³¡ç”µå­èŒ¶... âŒ› ç­‰å¾…é‡å­æ²¸è…¾",
	"ğŸ§ è„‘æ³¢é™å™ªä¸­... ğŸ¶ æ£€æµ‹åˆ°BGMã€Šå¤©ç©ºä¹‹åŸã€‹",
	"ğŸŒ åŠ é€Ÿèœ—ç‰›è¿›ç¨‹... ğŸï¸ å½“å‰æ—¶é€Ÿ0.5å…¬é‡Œ",
	"ğŸ“š åŠ è½½äººç”Ÿé‡å¼€æ¨¡æ‹Ÿå™¨... ğŸŒ  é€‰æ‹©ã€å½©è™¹å­¦è€…ã€èŒä¸š",
	"ğŸ›‹ï¸ æ£€æµ‹åˆ°èˆ’é€‚åŒº... âš ï¸ å»ºè®®ç»§ç»­ç˜«å",
	"ğŸ³ ç…è›‹ç‰©ç†å­¦ç ”ç©¶... â˜¢ï¸ å®Œç¾æºå¿ƒç‡99.9%",
	"ğŸ§˜ è¿›å…¥ç¦…å®šæ¨¡å¼... ğŸ™ 404çƒ¦æ¼æœªæ‰¾åˆ°",
	"ğŸ“Š åˆ†ææ‘¸é±¼æ•°æ®... ğŸ“ˆ ä»Šæ—¥æ•ˆç‡æ›²çº¿ï¼š__---~~~",
	"ğŸ å‡†å¤‡æ—¶ç©ºç›²ç›’... âœ¨ å†…å«å¹³è¡Œä¸–ç•Œçš„ä½ "
]
func idle_text():
	var effect: String = "[center]"
	if randf() < 0.1:
		var base_text = "è¯·ç¨ç­‰..." + rainbow_phrases[randi()%len(rainbow_phrases)] +\
						"(å‘ç°å½©è›‹ å¹¸è¿+1)"
		effect += idle_effects[-1].format([base_text])
	else:
		var base_text = "è¯·ç¨ç­‰......"
		effect += idle_effects[randi()%(len(idle_effects)-1)].format([base_text])
	effect += "[/center]"
	return effect


func _on_send_pressed() -> void:
	if !chat_bot.model:
		push_error("ChatBot has no model.")
		return
	send.disabled = true
	input_box.editable = false
	idle.text = idle_text()
	idle_pan.visible = true
	chat_bot.chat(input_box.text, user_agent)


func call_back(chat_id : int, _final_answer : String, chat_info : Dictionary):
	if chat_info.get("status") == "failed":
		idle.text = "Chat failed, Please check errors in the console output."
		push_error(chat_info.get("result"))
		await get_tree().create_timer(3).timeout
		idle_pan.visible = false
		input_box.editable = true
		send.disabled = false
		return
	var in_unit = CHAT_UNIT.instantiate()
	chat_list.add_child(in_unit)
	in_unit.chat_data = chat_info["input"]
	var out_unit = CHAT_UNIT.instantiate()
	chat_list.add_child(out_unit)
	out_unit.chat_data = chat_info["result"]["message"]
	input_box.text = ""
	idle_pan.visible = false
	input_box.editable = true
	send.disabled = false


# API config
@onready var type: OptionButton = $Config/APIConfig/Select/Type
@onready var llm: OptionButton = $Config/APIConfig/Select/LLM
@onready var cllm: LineEdit = $Config/APIConfig/Select/CLLM

@onready var url: LineEdit = $Config/APIConfig/APIUrl/Url
@onready var key: LineEdit = $Config/APIConfig/APIKey/Key

@onready var use_res: CheckBox = $Config/APIConfig/Path/UseRes
@onready var model_path: LineEdit = $Config/APIConfig/Path/Path

func refresh_model():
	type.clear()
	for t in ModelLayer.model_list.keys():
		type.add_item(t)

var _is_refresh : bool = false
func _on_type_pressed() -> void:
	if !_is_refresh:
		refresh_model()

func _on_type_item_selected(index: int) -> void:
	llm.clear()
	var model_list : Array = ModelLayer.model_list[type.text]
	if model_list.is_empty():
		llm.visible = false
		cllm.visible = true
		return
	llm.visible = true
	cllm.visible = false
	for m in model_list:
		llm.add_item(m)


func _on_use_res_toggled(toggled_on: bool) -> void:
	model_path.visible = toggled_on


func _on_save_pressed() -> void:
	if chat_bot.model:
		chat_bot.model.save_model(Default_Model_path)
		print("The currently used model has been set as default.")
	else:
		push_warning("Please set up the model for the Bot first.")


func _on_set_pressed() -> void:
	var model : BaseModel
	if use_res.button_pressed:
		if model_path.text.get_extension() != "tres":
			push_error("The file is not a resource file."+model_path.text.get_extension())
		else:
			var res = load(model_path.text)
			if res is BaseModel:
				model = res
			else:
				push_error("The file is not a model resource."+model_path.text)
	else:
		if type.text == "DEFAULT":
			if url.text.is_empty() or key.text.is_empty():
				model = ModelLayer.creat_model(cllm.text)
			else:
				model = ModelLayer.creat_model(cllm.text, url.text, key.text)
		else:
			if url.text.is_empty() or key.text.is_empty():
				model = ModelLayer.creat_model(llm.text)
			else:
				model = ModelLayer.creat_model(llm.text, url.text, key.text)
		url.text = model.api_url
		key.text = model.api_key
	chat_bot.model = model
	print("Model setted.")


# MCP config
@onready var server_list: VBoxContainer = $MCP/MCP/ServerBox/ServerList
const SERVER_UNIT = preload("res://addons/AIdot/Res/Agents/GodotAssistant/ServerUnit.tscn")

func get_tools():
	chat_bot.tool_bag.add_tool(ToolBox.get_box_tool())

func _on_add_server_pressed() -> void:
	var unit = SERVER_UNIT.instantiate()
	server_list.add_child(unit)
	unit.server_change.connect(get_tools)
